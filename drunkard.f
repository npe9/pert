      PROGRAM DRUNKARD
C  THIS PROGRAMME CARRIES OUT A MODIFIED RANDOM-WALK CALCULATION IN
C  WHICH THE EQUAL-LENGTH STEPS ARE ONLY ALONG THE X AND Y DIRECTIONS.
C  TO DEAL WITH A RANGE OF PROBLEMS THE PROBABILITIES ARE DEFINED AS 
C  FOR (1) THE DIRECTION OF THE LAST STEP,
C      (2) THE REVERSE OF THE DIRECTION OF THE LAST STEP
C  AND (3) AT RIGHT ANGLES TO THE DIRECTION OF THE LAST STEP
C          WHERE RIGHT AND LEFT DIRECTIONS HAVE EQUAL PROBABILITY.
C  THE ASSOCIATED PROBABILITIES ARE P(1), P(2) AND P(3).   ONLY P(1)
C  AND P(2) NEED TO BE DEFINED AND THEY CAN BE MODIFIED BY THE DATA
C  STATEMENT.
C
C  FOR THIS PROGRAMME A RANDOM NUMBER GENERATOR IS USED WHICH 
C  IS DERIVED FROM "NUMERICAL RECIPES" BY W.H.PRESS,B.P. FLANNERY,
C  S.A. TEULKOLSKY AND W.T. VETTERLING (C.U.P.)
C
C  FOR EACH VALUE OF N SPECIFIED THE ROOT-MEAN SQUARE DISTANCE OF
C  TRAVEL IS ESTIMATED FROM 1000 SEPARATE RANDOM WALKS.
      DIMENSION D(1000),P(2),CUM(4)
      DATA P,IDUM/0.25,0.25,-255/
      CUM(1)=P(1)
      CUM(2)=P(1)+P(2)
      CUM(4)=1.0
      CUM(3)=(CUM(2)+CUM(4))/2.0
      WRITE(6,'('' INPUT THE NUMBER OF STEPS, N '')')
      READ(5,*)N
      DO 1 I=1,1000
C  THE FIRST STEP IS TAKEN ARBITRARILY IN THE +X DIRECTION
      ID=0
C  ID=0 IF STEP IS IN THE POSITIVE X DIRECTION 
C  ID=2 IF STEP IS IN THE NEGATIVE X DIRECTION
C  ID=1 IF STEP IS IN THE POSITIVE Y DIRECTION
C  ID=3 IF STEP IS IN THE NEGATIVE Y DIRECTION.
      SUMX=1
      SUMY=0     
      DO 2 J=1,N-1
      R=RAN2(IDUM)
      IADD=3
      IF(CUM(3).GT.R)IADD=1
      IF(CUM(2).GT.R)IADD=2
      IF(CUM(1).GT.R)IADD=0
      ID=MOD(ID+IADD,4)
      IDP=ID+1
      GOTO(3,4,5,6)IDP
C POSITIVE X DIRECTION     
    3 SUMX=SUMX+1
      GOTO 2
C POSITIVE Y DIRECTION
    4 SUMY=SUMY+1
      GOTO 2
C NEGATIVE X DIRECTION
    5 SUMX=SUMX-1
      GOTO 2
C NEGATIVE Y DIRECTION
    6 SUMY=SUMY-1
    2 CONTINUE        
      D(I)=SUMX**2+SUMY**2
    1 CONTINUE
      TOT=0
      DO 10 I=1,1000
      TOT=TOT+D(I)
   10 CONTINUE
      TOT=SQRT(TOT/1000.0)
      WRITE(6,100)N,TOT
  100 FORMAT(25H THE AVERAGE DISTANCE FOR,I5,9H STEPS IS,F9.2)
      STOP
      END

      
      FUNCTION RAN2(IDUM)
      PARAMETER(M=714025,IA=1366,IC=150889,RM=1./M)
      DIMENSION IR(97)
      DATA IFF/0/
      IF(IDUM.LT.0.OR.IFF.EQ.0)THEN
      IFF=1
      IDUM=MOD(IC-IDUM,M)
      DO 11 J=1,97
      IDUM=MOD(IA*IDUM+IC,M)
      IR(J)=IDUM
   11 CONTINUE
      IDUM=MOD(IA*IDUM+IC,M)
      IY=IDUM
      ENDIF
      J=1+(97*IY)/M
      IF(J.GT.97.OR.J.LT.1)PAUSE
      IY=IR(J)
      RAN2=IY*RM
      IDUM=MOD(IA*IDUM+IC,M)
      IR(J)=IDUM
      RETURN
      END

      
