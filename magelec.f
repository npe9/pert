      PROGRAM MAGELEC
C  THIS PROGRAM COMPUTES THE TRAJECTORY OF AN ELECTRON IN THE X,Y PLANE
C  SUBJECT TO A MAGNETIC FIELD B(X,Y) IN THE Z DIRECTION.   B(X,Y) IS
C  DEFINED BY A STATEMENT FUNCTION.   THE USER IS ASKED TO INPUT THE 
C  INITIAL POSITION (X,Y) AND THE INITIAL VELOCITY COMPONENTS (VX,VY).
C  THE OUTPUT IS IN A FILE 'EPATH.DAT' BUT THE NAME OF THE FILE MAY BE
C  CHANGED BY THE USER.  THIS FILE MAY BE PRINTED OR SAVED AS INPUT TO
C  A GRAPHICS PROGRAM.
      DIMENSION X(0:100),Y(0:100),VX(0:100),VY(0:100)
      CHARACTER ANS*1, FNAME*10
C  SET VALUE OF E/M FOR ELECTRON
      EOM=1.759E11
C  SET THE NAME OF THE OUTPUT FILE FOR (X,Y) VALUES
      FNAME='EPATH.DAT'
C  INPUT INITIAL VALUES OF X, Y, VX, VY
      WRITE(6,'('' INPUT INITIAL VALUES OF X AND Y '')')
      READ(5,*)X(0),Y(0)
      WRITE(6,'('' INPUT INITIAL VALUES OF VX, AND VY '')')
      READ(5,*)VX(0),VY(0)
C  INPUT THE TIMESTEP
      WRITE(6,'('' INPUT THE TIMESTEP'')')
      READ(5,*)DT
C  START CALCULATION
      DO 1 I=1,100
C  PREDICT THE POSITION OF THE ELECTRON AT THE END OF THE TIMESTEP.
C  THIS WILL BE USED TO GET AN ESTIMATE OF THE AVERAGE MAGNETIC FIELD 
C  DURING THE TIMESTEP.
      XPRED=X(I-1)+VX(I-1)*DT
      YPRED=Y(I-1)+VY(I-1)*DT
C  ESTIMATE OF AVERAGE FIELD IN THE TIME INTERVAL
      BAV=0.5*(B(X(I-1),Y(I-1))+B(XPRED,YPRED))
C  CALCULATE VALUE OF PSI [EQUATIONS (3.18) & (3.19)]
      PSI=0.5*EOM*BAV*DT
C  CALCULATE COEFFICIENT FOR EQUATIONS (3.18) & (3.19)
      Z1=1-PSI*PSI
      Z2=2-Z1
      C1=Z1/Z2
      C2=PSI/Z2                   
C  ADVANCE VALUES OF X, Y, VX, AND VY.
      X(I)=X(I-1)+DT*(VX(I-1)/Z2+C2*VY(I-1))
      Y(I)=Y(I-1)+DT*(VY(I-1)/Z2-C2*VX(I-1))
      VX(I)=C1*VX(I-1)+2.0*C2*VY(I-1)
      VY(I)=C1*VY(I-1)-2.0*C2*VX(I-1)
    1 CONTINUE
    4 WRITE(6,'(''THE VALUES OF X & Y WILL BE OUTPUT IN A FILE '')')
      WRITE(6,'(''EPATH.DAT.  DO YOU WANT TO CHANGE THE NAME OF'')')
      WRITE(6,'(''THE FILE?  [Y/N]'')')  
      READ(5,50)ANS 
   50 FORMAT(A1)
      IF(ANS.EQ.'N'.OR.ANS.EQ.'n')GOTO 2
      IF(ANS.EQ.'Y'.OR.ANS.EQ.'y')GOTO 3
      GOTO 4
    3 WRITE(6,'(''INPUT NAME OF THE OUTPUT FILE [<= 10 CHARACTERS]'')')  
      READ(5,100)FNAME
  100 FORMAT(A10)
    2 OPEN(UNIT=11,FILE=FNAME)
      DO 20 I=0,100
      WRITE(11,*)X(I),Y(I)
   20 CONTINUE
    7 WRITE(6,'(''DO YOU WANT PRINTED OUTPUT? [Y/N] '')')
      READ(5,50)ANS
      IF(ANS.EQ.'N'.OR.ANS.EQ.'n')GOTO 5
      IF(ANS.EQ.'Y'.OR.ANS.EQ.'y')GOTO 6
      GOTO 7
    6 OPEN(UNIT=9,FILE='LPT1')
      WRITE(6,200)(I,X(I),Y(I),I=0,80)
  200 FORMAT(3(I6,F8.5,F8.5))
    5 STOP
      END       
      FUNCTION B(X,Y)
      DATA D/0.5/
      B=D*Y
      END
