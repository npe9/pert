      PROGRAM OSCILLATE
      REAL M,K,X(0:1000),T(0:1000),WT(4), DX(0:4),DU(0:4)
      CHARACTER ANS*1
      DATA WT/0.0,0.5,0.5,1.0/
C  THE FOLLOWING DATA IS FOR A STANDARD PROBLEM.  HOWEVER, THE
C  USER CAN REPLACE ANY OR ALL OF THESE VALUES.
      DATA M,F,K,FF,WC,X0,U0/1.0E-3,3.0E-3,100.0,1.0E-2,300.0,0,0/
      PI=4.0*ATAN(1.0)
      OPEN(UNIT=9,FILE='LPT1')
C  OPEN FILES FOR GRAPHICAL DATA
      OPEN(UNIT=21,FILE='OSC1.DAT')
      OPEN(UNIT=22,FILE='OSC2.DAT')
      OPEN(UNIT=23,FILE='OSC3.DAT')
      OPEN(UNIT=24,FILE='OSC4.DAT')
      OPEN(UNIT=25,FILE='OSC5.DAT')
      OPEN(UNIT=26,FILE='OSC6.DAT')
   8  WRITE(6,'(''THE CURRENT VALUES OF VARIOUS PARAMETERS ARE:'')')
      WRITE(6,201)M
 201  FORMAT(16H 1. MASS [m] IS ,F8.4,3H KG)
      WRITE(6,202)F
 202  FORMAT(28H 2. DAMPING CONSTANT [f] IS ,F8.4,5H Ns/m)
      WRITE(6,203)K
 203  FORMAT(36H 3. RESTORING FORCE CONSTANT [k] IS ,F8.2,4H N/m)
      WRITE(6,204)FF
 204  FORMAT(43H 4. FORCING VIBRATION HAS AMPLITUDE [F] IS ,F8.4,2H N)
      WRITE(6,205)WC
 205  FORMAT(31H 5. AND ANGULAR FREQUENCY [wc] ,F8.4,3H /s)
      WRITE(6,206)X0
 206  FORMAT(32H 6. THE INITIAL POSITION [x0] = ,F8.4,2H m)
      WRITE(6,207)U0
 207  FORMAT(36H 7. AND THE INITIAL VELOCITY [u0] = ,F8.4,4H m/s)
C  THE USER MAY NOW CHANGE THESE VALUES IF REQUIRED
    3 WRITE(6,'(''DO YOU WANT TO CHANGE ANY OF THESE?  Y/N '')')
      READ(5,100)ANS
  100 FORMAT(A1)
      IF(ANS.EQ.'N'.OR.ANS.EQ.'n')GOTO 1
      IF(ANS.EQ.'Y'.OR.ANS.EQ.'y')GOTO 2
      GOTO 3    
    2 WRITE(6,'(''INDICATE BY NUMBER [1 TO 7] THE ONE TO CHANGE'')')
      READ(5,*)NO
      GOTO(21,22,23,24,25,26,27)NO
   21 WRITE(6,'(''TYPE IN IN THE NEW VALUE OF m'')')
      READ(5,*)M
      GOTO 8
   22 WRITE(6,'(''TYPE IN IN THE NEW VALUE OF f'')')
      READ(5,*)F
      GOTO 8
   23 WRITE(6,'(''TYPE IN IN THE NEW VALUE OF k'')')
      READ(5,*)K
      GOTO 8
   24 WRITE(6,'(''TYPE IN IN THE NEW VALUE OF F'')')
      READ(5,*)FF
      GOTO 8
   25 WRITE(6,'(''TYPE IN IN THE NEW VALUE OF wc'')')
      READ(5,*)WC
      GOTO 8
   26 WRITE(6,'(''TYPE IN IN THE NEW VALUE OF x0'')')
      READ(5,*)X0
      GOTO 8
   27 WRITE(6,'(''TYPE IN IN THE NEW VALUE OF u0'')')
      READ(5,*)U0
      GOTO 8
C  THE VALUE OF ALPHA IS NOW TYPED IN
    1 WRITE(6,'(''TYPE IN THE VALUE OF ALPHA'')')
      READ(5,*)ALPHA
C  PRINT THE PARAMETERS FOR THE PROBLEM
      WRITE(9,101)M,F,K,FF
  101 FORMAT(5H m = ,E10.5,5H f = ,E10.5,5H k = ,E10.5,5H F = ,E10.5)
      WRITE(9,102)WC,X0,U0
  102 FORMAT(6H wc = ,E10.5,6H x0 = ,E10.5,6H u0 = ,E10.5)    
      WRITE(9,103)ALPHA
  103 FORMAT(9H ALPHA = ,F6.3)    
      WRITE(6,'(''THE PROBLEM IS NOW DEFINED SO INTEGRATION CAN '')')
      WRITE(6,'(''BEGIN. THE PROGRAM IS RUN IN BATCHES OF 10 '')')
      WRITE(6,'(''CYCLES OF THE FORCING VIBRATION.  AFTER EACH '')')
      WRITE(6,'(''BATCH THE USER MAY ASK FOR THE DISPLACEMENT-TIME'')') 
      WRITE(6,'(''VALUES TO BE OUTPUT TO A DATA FILE OSCn.DAT '')')
      WRITE(6,'(''WHERE n RUNS FROM 1 TO 6.  IF A SEVENTH OUTPUT '')')
      WRITE(6,'(''IS REQUESTED THEN THIS WILL OVERWRITE THE FIRST '')')
      WRITE(6,'(''DATA FILE, AN EIGHTH REQUEST WILL OVERWRITE THE '')')
      WRITE(6,'(''SECOND DATA FILE AND SO ON.'')')
      NOUT=-1  
      NC1=0
C  THE TIMESTEP IS SET AT ONE HUNDRETH OF THE PERIOD OF THE FORCING
C  VIBRATION.  THIS IS MORE THAN NEEDED TO FOLLOW THE FORCING VIBRATION
C  BUT ALLOWS A FAST TRANSIENT TO BE FOLLOWED IF IT OCCURS.
      H=PI/50.0/WC
      T0=0
   64 X(0)=X0
      T(0)=T0
      DO 4 J=1,1000
      T(J)=T(0)+H*J
      DO 5 I=1,4
      TX=T(J-1)+WT(I)*H
      XX=X0+WT(I)*DX(I-1)
      UX=U0+WT(I)*DU(I-1)
      DX(I)=H*UX
C  GUARD AGAINST ZERO UX SINCE 0**0 IS NOT DEFINED
      IF(ABS(UX).LT.1.0E-16)THEN
      Z=0
      ELSE
      Z=ABS(UX)**(ALPHA-1.0)*UX
      ENDIF
      DU(I)=H*(FF*COS(WC*TX)-F*Z-K*XX)/M
    5 CONTINUE
      X0=X0+(DX(1)+DX(4)+2.0*(DX(2)+DX(3)))/6.0
      U0=U0+(DU(1)+DU(4)+2.0*(DU(2)+DU(3)))/6.0
      X(J)=X0
    4 CONTINUE  
   92 NC2=NC1+10
      WRITE(6,300)NC1,NC2
  300 FORMAT(35H DO YOU WANT A DATA FILE FOR CYCLES,I4,4H TO ,I4,1H?) 
      READ(5,50)ANS
      IF(ANS.EQ.'N'.OR.ANS.EQ.'n')GOTO 60
      IF(ANS.EQ.'Y'.OR.ANS.EQ.'y')GOTO 91
      GOTO 92
   91 NOUT=NOUT+1
      NN=MOD(NOUT,6)+21
      REWIND NN
      DO 93 I=0,1000
      WRITE(NN,*)T(I),X(I)
   93 CONTINUE
   60 WRITE(6,'('' DO YOU WISH TO CONTINUE? [Y/N] '')')
      READ(5,50)ANS
      IF(ANS.EQ.'N'.OR.ANS.EQ.'n')GOTO 999
      IF(ANS.EQ.'Y'.OR.ANS.EQ.'y')GOTO 61
      GOTO 60
   50 FORMAT(A1)
   61 X0=X(1000)
      T0=T(1000)
      NC1=NC1+10
C  PROVISION IS MADE FOR PRINTED NUMERICAL OUTPUT.  TO ECONOMIZE THIS 
C  IS FOR EVERY FIFTH TABLE ENTRY.
   68 WRITE(6,'('' DO YOU WISH TO PRINT THE DISPLACEMENT AND TIME'')')
      WRITE(6,'('' VALUES FOR THE LAST CYCLE?  [Y/N]'')')
      READ(5,50)ANS
      IF(ANS.EQ.'N'.OR.ANS.EQ.'n')GOTO 66
      IF(ANS.EQ.'Y'.OR.ANS.EQ.'y')GOTO 67
      GOTO 68
   67 WRITE(9,200)(T(I),X(I),I=1,1000,5)
  200 FORMAT(3(F10.4,E13.4))
   66 GOTO 64
  999 STOP
      END
