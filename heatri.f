      PROGRAM HEATRI
C  THIS GIVES AN EXPLICIT SOLUTION FOR TEMPERATURES ALONG A BAR
C  WITH TRUNCATED CONICAL CROSS SECTION WITH THE ENDS AT FIXED
C  TEMPERATURES.
C  N IS THE NUMBER OF DIVISIONS OF THE BAR FOR THE CALCULATION
C  M IS THE NUMBER OF DIVISIONS OF THE BAR FOR OUTPUT VALUES
C  NOTE: N SHOULD BE A MULTIPLE OF M
C  CAPPA IS THE THERMAL CONDUCTIVITY OF THE BAR IN SI UNITS
C  THE LENGTH OF THE BAR IS XL
C  T0 AND TN ARE THE FIXED TEMPERATURES AT THE TWO ENDS OF THE BAR
      PARAMETER (N=100)
      REAL A(3,N-1),C(N-1),Y(N-1)
      DATA T0,TN,M/500,300,10/
      DATA CAPPA,XL,PI/200,1.0,3.141592654/            
      K=N/M
C  CLEAR ARRAYS
      DO 5 I=1,N-1
      C(I)=0
      DO 6 J=1,3
      A(J,I)=0
    6 CONTINUE
    5 CONTINUE
C  SET UP THE COEFFICIENTS OF THE TRIDIAGONAL MATRIX
      DO 1 I=1,N-1
      IF(I.EQ.1)GOTO 2
      A(1,I)=(2.0-(I-0.5)/N)**2
    2 A(2,I)=-(2.0-(I+0.5)/N)**2-(2.0-(I-0.5)/N)**2
      IF(I.EQ.N-1)GOTO 1
      A(3,I)=(2.0-(I+0.5)/N)**2
    1 CONTINUE
C  SET UP COEFFICIENTS OF RIGHT-HAND-SIDE VECTOR
      C(1)=-(2-0.5/N)**2*T0
      C(N-1)=-(2.0-(N-0.5)/N)**2*TN
      CALL TRIDIAG(A,C,Y,N-1)
C  NOW CALCULATE HEAT FLOW THROUGH BAR
      Q=CAPPA*PI*(2-0.5/N)**2*(T0-Y(1))*1.0E-4*N/XL
C  OUTPUT RESULTS
      WRITE(6,100)(I,Y(I),I=K,N-1,K)
  100 FORMAT(3(I4,F8.2))
      WRITE(6,200)Q
  200 FORMAT(18H THE HEAT FLOW IS ,F10.3)                 
      STOP
      END
